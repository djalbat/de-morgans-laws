Metavariable P:Statement
Metavariable Q:Statement

Rule (NegatedConjuctionInfersDisjunctionOfNegations)
  Premise
   ¬(P ∧ Q)
  Conclusion
    ¬P ∨ ¬Q
  Proof
    Suppose
      ¬(¬P ∨ ¬Q)
    Hence
      Suppose
        P
      Hence
        Suppose
          Q
        Hence
          P
          Q
        Then
          P ∧ Q by ConjunctionIntroduction
        Q ⇒ (P ∧ Q) by ImplicationIntroduction
        ¬(P ∧ Q)
        ¬Q by ModusTollens
      Then
        ¬P ∨ ¬Q by DisjunctionLeftIntroduction
      P ⇒ (¬P ∨ ¬Q) by ImplicationIntroduction
      ¬P by ModusTollens
    Then
      ¬P ∨ ¬Q by DisjunctionRightIntroduction
    ¬(¬P ∨ ¬Q) ⇒ (¬P ∨ ¬Q) by ImplicationIntroduction
    ¬(¬P ∨ ¬Q) ⇒ ¬(¬P ∨ ¬Q) by TrivialImplication
    ¬¬(¬P ∨ ¬Q) by NegationIntroduction
  Therefore
    ¬P ∨ ¬Q by DoubleNegationElimination

Rule (NegatedConjunctionOfNegationsInfersDisjunction)
  Premise
    ¬(¬P ∧ ¬Q)
  Conclusion
    P ∨ Q
  Proof
    Suppose
      Q
    Then
      P ∨ Q by DisjunctionLeftIntroduction
    Q ⇒ (P ∨ Q) by ImplicationIntroduction
    Suppose
      ¬Q
    Hence
      Suppose
        ¬P
      Hence
        ¬Q
      Then
        ¬P ∧ ¬Q by ConjunctionIntroduction
      ¬P ⇒ (¬P ∧ ¬Q) by ImplicationIntroduction
      ¬(¬P ∧ ¬Q)
      ¬¬P by ModusTollens
      P by DoubleNegationElimination
    Then
      P ∨ Q by DisjunctionRightIntroduction
    ¬Q ⇒ (P ∨ Q) by ImplicationIntroduction
    Q ∨ ¬Q by TheLawOfTheExludedMiddle
  Therefore
    P ∨ Q by DisjunctionElimination

Rule (NegatedDisjunctionOfNegationsInfersConjunction)
  Premise
    ¬(¬P ∨ ¬Q)
  Conclusion
    P ∧ Q
  Proof
    Suppose
      ¬(P ∧ Q)
    Then
      ¬P ∨ ¬Q by NegatedConjuctionInfersDisjunctionOfNegations
    ¬(P ∧ Q) ⇒ (¬P ∨ ¬Q) by ImplicationIntroduction
    ¬¬(P ∧ Q) by ModusTollens
  Therefore
    P ∧ Q by DoubleNegationElimination

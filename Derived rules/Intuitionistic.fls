Metavariable P:Statement
Metavariable Q:Statement
Metavariable R:Statement

Rule (ConjunctionInfersNegatedDisjunctionOfNegations)
  Premise
    P ∧ Q
  Conclusion
    ¬(¬P ∨ ¬Q)
  Proof
    Suppose
      ¬P ∨ ¬Q
    Then
      Q by ConjunctionLeftElimination
    (¬P ∨ ¬Q) ⇒ Q by ImplicationIntroduction
    Suppose
      ¬P ∨ ¬Q
    Hence
      P by ConjunctionRightElimination
      ¬P ∨ ¬Q
      P ∧ (¬P ∨ ¬Q) by ConjunctionIntroduction
      (P ∧ ¬P) ∨ (P ∧ ¬Q) by ConjunctionDistributesOverDisjunction
      (P ∧ ¬Q) ∨ (P ∧ ¬P) by DisjunctionCommutes
      P ∧ ¬Q by ContradictionElimination
    Then
      ¬Q by ConjunctionLeftElimination
    (¬P ∨ ¬Q) ⇒ ¬Q by ImplicationIntroduction
  Therefore
    ¬(¬P ∨ ¬Q) by NegationIntroduction

Rule (DisjunctionInfersNegatedConjunctionOfNegations)
  Premise
    P ∨ Q
  Conclusion
    ¬(¬P ∧ ¬Q)
  Proof 
    Suppose
      ¬P ∧ ¬Q
    Hence
      ¬Q by ConjunctionLeftElimination
      P ∨ Q
      ¬Q ∧ (P ∨ Q) by ConjunctionIntroduction
      (¬Q ∧ P) ∨ (¬Q ∧ Q) by ConjunctionDistributesOverDisjunction
      ((¬Q ∧ P) ∨ ¬Q) ∧ ((¬Q ∧ P) ∨ Q) by DisjunctionDistributesOverConjunction
      ((¬Q ∧ P) ∨ Q) ∧ ((¬Q ∧ P) ∨ ¬Q) by ConjunctionCommutes
      (¬Q ∧ P) ∨ (Q ∧ ¬Q) by DisjunctionDistributedOverConjunction
      ¬Q ∧ P by ContradictionElimination
    Then
      P by ConjunctionLeftElimination
    (¬P ∧ ¬Q) ⇒ P by ImplicationIntroduction
    Suppose
      ¬P ∧ ¬Q
    Then
      ¬P by ConjunctionRightElimination
    (¬P ∧ ¬Q) ⇒ ¬P by ImplicationIntroduction
  Therefore
    ¬(¬P ∧ ¬Q) by NegationIntroduction

Rule (DisjunctionOfNegationsInfersNegatedConjuction)
  Premise
    ¬P ∨ ¬Q
  Conclusion
    ¬(P ∧ Q)
  Proof
    Suppose
      P ∧ Q
    Then
      Q by ConjunctionLeftElimination
    (P ∧ Q) ⇒ Q by ImplicationIntroduction
    Suppose
      P ∧ Q
    Hence
      P by ConjunctionRightElimination
      ¬P ∨ ¬Q
      P ∧ (¬P ∨ ¬Q) by ConjunctionIntroduction
      (P ∧ ¬P) ∨ (P ∧ ¬Q) by ConjunctionDistributesOverDisjunction
      (P ∧ ¬Q) ∨ (P ∧ ¬P) by DisjunctionCommutes
      P ∧ ¬Q by ContradictionElimination
    Then
      ¬Q by ConjunctionLeftElimination
    (P ∧ Q) ⇒ ¬Q by ImplicationIntroduction
  Therefore
    ¬(P ∧ Q) by NegationIntroduction

Rule (ConjunctionOfNegationsInfersNegatedDisjunction)
  Premise
    ¬P ∧ ¬Q
  Conclusion
    ¬(P ∨ Q)
  Proof
    ¬P by ConjunctionRightElimination
    ¬Q by ConjunctionLeftElimination
    Suppose
      P ∨ Q
    Hence
      ¬P
      P ⇒ R by PrincipleOfExplosion
      ¬Q
      Q ⇒ R by PrincipleOfExplosion
    Then
      R by DisjunctionElimination
    (P ∨ Q) ⇒ R by ImplicationIntroduction
    Suppose
      P ∨ Q
    Hence
      ¬P
      P ⇒ ¬R by PrincipleOfExplosion
      ¬Q
      Q ⇒ ¬R by PrincipleOfExplosion
    Then
      ¬R by DisjunctionElimination
    (P ∨ Q) ⇒ ¬R by ImplicationIntroduction
  Therefore
    ¬(P ∨ Q) by NegationIntroduction

Rule (NegatedDisjunctionInfersConjuctionOfNegations)
  Premise
    ¬(P ∨ Q)
  Conclusion
    ¬P ∧ ¬Q
  Proof
    Suppose
      P
    Hence
      P ∨ Q by DisjunctionRightIntroduction
      ¬(P ∨ Q)
    Then
      (P ∨ Q) ∧ ¬(P ∨ Q) by ConjunctionIntroduction
    P ⇒ ((P ∨ Q) ∧ ¬(P ∨ Q)) by ImplicationIntroduction
    P ⇒ (P ∨ Q) by ConjunctionRightEliminationWithImplication
    P ⇒ ¬(P ∨ Q) by ConjunctionLeftEliminationWithImplication
    ¬P by NegationIntroduction
    Suppose
      Q
    Hence
      P ∨ Q by DisjunctionLeftIntroduction
      ¬(P ∨ Q)
    Then
      (P ∨ Q) ∧ ¬(P ∨ Q) by ConjunctionIntroduction
    Q ⇒ ((P ∨ Q) ∧ ¬(P ∨ Q)) by ImplicationIntroduction
    Q ⇒ (P ∨ Q) by ConjunctionRightEliminationWithImplication
    Q ⇒ ¬(P ∨ Q) by ConjunctionLeftEliminationWithImplication
    ¬Q by NegationIntroduction
    ¬P
    ¬Q
  Therefore
    ¬P ∧ ¬Q by ConjunctionIntroduction
